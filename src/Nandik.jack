class Nandik {

    field int location, road, tick, maxTick, roadPosition;
    field boolean alive;

    constructor Nandik new(int road1, int maxTick1) {

        let road = road1;
        if (road = 1) { let location = 4357; }
        if (road = 2) { let location = 4378; }
        if (road = 3) { let location = 5638; }
        if (road = 4) { let location = 5657; }

        do Drawing.drawNandik(location);

        let tick = maxTick1;
        let maxTick = maxTick1;
        let roadPosition = 0;
        let alive = true;

        return this;
    }

    method int getRoadPosition(){
        return roadPosition;
    }

    method void tryMove(Aleksey aleksey) {
        let tick = tick - 1;
        if (tick = 0) {
            let tick = maxTick;
            do move(aleksey);
        }
        return;
    }

    method void move(Aleksey aleksey) {
        var int rotation;

        let roadPosition = roadPosition + 1;
        if (roadPosition = 7) {
            
            // "kill" nandik
            let alive = false;
            do Clearing.clearNandik(location);

            // register results
            do Output.moveCursor(0, 0);
            do Output.printString("    ");
            do Output.moveCursor(0, 0);
            let rotation = aleksey.getRotation();
            if (rotation = road) {
                do Output.printString("OK");
                return;
            }
            do Output.printString("FAIL");
            return;
        }

        do Clearing.clearNandik(location);
        if ((road = 1) | (road = 3)) { let location = location + 1; }
        if ((road = 2) | (road = 4)) { let location = location - 1; }
        do Drawing.drawNandik(location);
        return;
    }

    method boolean isAlive() {
        return alive;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
}