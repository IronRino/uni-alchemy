class GameField {

    function void run() {

        //----------------  VAR SETUP  -----------------//

        var char key, previousKey;

        var int i;
        var Array objects;
        var int objectCount;
        var Nandik nandik;
        var boolean nandikAlive;

        var Aleksey aleksey;

        var int road, roadPosition;
        var RandomGenerator rng;

        var int moveTick;
        var int maxSpawnTick;
        var int spawnTick;

        var boolean exit;

        //---------------  GAME SETUP  -----------------//

        let key = 0;
        let previousKey = 0;

        let objects = Array.new(25);
        let objectCount = 0;

        let aleksey = Aleksey.new();
        let rng = RandomGenerator.new(91); 

        let moveTick = 100;
        let maxSpawnTick = 200;
        let spawnTick = maxSpawnTick;

        let exit = false;

        //---------------  GAME LOOP  ------------------//

        while (~exit) {
            do Sys.wait(10);

            if (spawnTick = 0) {
                let road = rng.random() + 1;
                let objects[objectCount] = Nandik.new(road, moveTick);
                let objectCount = objectCount + 1;
                let spawnTick = maxSpawnTick;
            }

            let spawnTick = spawnTick - 1;

            let previousKey = key;
            let key = Keyboard.keyPressed();
            let rng = RandomGenerator.new(key);

            if (~(key = previousKey)) { do aleksey.clearHands(); }

            if (~((key = 140) | (key = 87) | (key = 76) | (key = 83) | (key = 80))) { do aleksey.setRotation(0); }
            if (key = 87) { do aleksey.setRotation(1); } // W
            if (key = 80) { do aleksey.setRotation(2); } // P
            if (key = 83) { do aleksey.setRotation(3); } // S
            if (key = 76) { do aleksey.setRotation(4); } // L
            if (key = 140) { let exit = true; } // Escape
            
            let i = 0;
            while (i < objectCount) {
                let nandik = objects[i];
                let nandikAlive = nandik.isAlive();
                if (nandikAlive) { do nandik.tryMove(aleksey); }
                let i = i + 1;
            }

            if (objectCount > 20) {
                let i = 0;
                while (i < objectCount) {
                    let nandik = objects[i];
                    do nandik.dispose();
                    let i = i + 1;
                }
                do objects.dispose();
                let objects = Array.new(25);
                let objectCount = 0;
            }

            do aleksey.rotate();

            if (maxSpawnTick > 10){
                let maxSpawnTick = maxSpawnTick - 1;
            }
        }
        return;
    }

    function void initialize() {
        do Drawing.drawRoad(4869);
        do Drawing.drawRoad(4884);
        do Drawing.drawRoad(6150);
        do Drawing.drawRoad(6163);
        do Drawing.drawBoxPlatform(7178);
        do Drawing.drawAlekseyTorso(4879);
        do Drawing.drawAlekseyHeadRight(3823);
        return;
    }
}