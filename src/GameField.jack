class GameField {

    function void run() {

        var char key, previousKey;

        var Array objects;
        var int objectCount;
        var int i;

        var Aleksey aleksey;

        var Nandik nandik;

        var int road, roadPosition;
        var RandomGenerator rng;

        var int moveTick;
        var int maxSpawnTick;
        var int spawnTick;

        var boolean exit;

        let key = 0;
        let previousKey = 0;

        let objects = Array.new(1000);
        let objectCount = 0;

        let aleksey = Aleksey.new();
        let rng = RandomGenerator.new(91); 

        let moveTick = 100;
        let maxSpawnTick = 200;
        let spawnTick = maxSpawnTick;

        let exit = false;

        while (~exit) {
            do Sys.wait(10);

            if (spawnTick = 0) {
                let road = rng.random() + 1;
                let objects[objectCount] = Nandik.new(road, moveTick);
                let objectCount = objectCount + 1;
                let spawnTick = maxSpawnTick;
            }

            let spawnTick = spawnTick - 1;

            let previousKey = key;
            let key = Keyboard.keyPressed();
            let rng = RandomGenerator.new(key);

            if (~(key = previousKey)) { do aleksey.clearHands(); }

            if (~((key = 140) | (key = 87) | (key = 76) | (key = 83) | (key = 80))) { do aleksey.setRotation(0); }
            if (key = 87) { do aleksey.setRotation(1); } // W
            if (key = 80) { do aleksey.setRotation(2); } // P
            if (key = 83) { do aleksey.setRotation(3); } // S
            if (key = 76) { do aleksey.setRotation(4); } // L
            if (key = 140) { let exit = true; } // Escape
            
            let i = 0;

            while (i < objectCount) {
                let nandik = objects[i];
                if (~(nandik = null)){
                    do nandik.tryMove(aleksey); 
                    let roadPosition = nandik.getRoadPosition();
                    if (roadPosition = 7){
                        do nandik.dispose();
                        let objects[i] = null; 
                    }
                }
                let i = i + 1;
            }

            do aleksey.rotate();
        }
        return;
    }

    function void initialize() {
        do Drawing.drawRoad(4869);
        do Drawing.drawRoad(4884);
        do Drawing.drawRoad(6150);
        do Drawing.drawRoad(6163);
        do Drawing.drawBoxPlatform(7178);
        do Drawing.drawAlekseyTorso(4879);
        do Drawing.drawAlekseyHeadRight(3823);
        return;
    }
}