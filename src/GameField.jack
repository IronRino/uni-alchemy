class GameField {

    function void run(int seed) {

        var char key, previousKey;

        var Score score;

        var Aleksey aleksey;
        var int alekseyRotation;

        var Random2 rng2;
        var int rngIndex;
        var int randomValue;
        var int road, roadPosition;

        var Obstacle obstacle;
        var boolean obstacleShouldMove, obstacleCanSpawn, isNandik;
        var int moveTick;

        var int time;
        var int maxTick;
        var int tick;
        var boolean exit;


        let key = 3;
        let previousKey = 3;

        let score = Score.new();

        let aleksey = Aleksey.new();

        let rng2 = Random2.new();
        let rngIndex = seed;
        let randomValue = 0;

        let obstacleCanSpawn = true;
        let moveTick = 51;

        let time = 60;
        let maxTick = 70;
        let tick = maxTick;
        let exit = false;
        

        while (~exit) {

            do Sys.wait(10);
            
            // time ticking down
            let tick = tick - 1;
            if (tick = 0) {
                let time = time - 1;
                let tick = maxTick;

                // update time
                if (time = 9) {
                    do Output.moveCursor(1, 6);
                    do Output.printString("   ");
                }
                do Output.moveCursor(1, 6);
                do Output.printInt(time);

                // progress game difficulty
                if (time = 55) { let moveTick = 40; }
                if (time = 50) { let moveTick = 30; }
                if (time = 45) { let moveTick = 20; }
                if (time = 40) { let moveTick = 25; }
                if (time = 35) { let moveTick = 10; }
                if (time = 30) { let moveTick = 8; }
                if (time = 25) { let moveTick = 6; }
                if (time = 20) { let moveTick = 5; }
                if (time = 15) { let moveTick = 4; }
                if (time = 10) { let moveTick = 3; }
                if (time = 5) { let moveTick = 2; }
            }

            // when time runs out - end game
            if (time = 0) { let exit = true; }

            // spawning things
            if (obstacleCanSpawn) {
                let road = rng2.getRoad(rngIndex) - 48;
                let randomValue = rng2.getObstacle(rngIndex);
                if (randomValue = 48) { let isNandik = false; }
                else { let isNandik = true; }

                let obstacle = Obstacle.new(road, moveTick, isNandik);
                let obstacleCanSpawn = false;

                let rngIndex = rngIndex + 1;
                if (rngIndex > 255) { let rngIndex = 0; }
            }
           
            // read key stuff
            let previousKey = key;
            let key = Keyboard.keyPressed();
            if (~(key = previousKey)) { do aleksey.clearHands(); }

            if (key = 140) { let exit = true; } // Escape

            if (~((key = 140) | (key = 87) | (key = 76) | (key = 83) | (key = 80))) { do aleksey.setRotation(0); }
            if (key = 87) { do aleksey.setRotation(1); } // W
            if (key = 80) { do aleksey.setRotation(2); } // P
            if (key = 83) { do aleksey.setRotation(3); } // S
            if (key = 76) { do aleksey.setRotation(4); } // L
            let alekseyRotation = aleksey.getRotation();

            // obstacle management
            if (~(obstacle = null)) { 
                let obstacleShouldMove = obstacle.tryMove(); 
                if (obstacleShouldMove) { do obstacle.move(alekseyRotation, score); }
                
                let roadPosition = obstacle.getRoadPosition();
                if (roadPosition = 7) { 
                    do obstacle.dispose(); 
                    let obstacle = null;
                    let obstacleCanSpawn = true;
                }
            }

            do aleksey.rotate();
        }

        do Screen.clearScreen();

        do Output.moveCursor(22, 0);
        do Output.printString("by vitvit & SanAndreas & kaba4ok");
        do Output.moveCursor(7, 26);
        do Output.printString("Time's up!");
        do Output.moveCursor(14, 23);
        do Output.printString("Your score: ");

        let randomValue = score.getScore();
        do Output.printInt(randomValue);

        do score.dispose();
        do aleksey.dispose();
        do rng2.dispose();
        return;
    }

    function void initialize() {
        do Drawing.drawRoad(4869);
        do Drawing.drawRoad(4884);
        do Drawing.drawRoad(6150);
        do Drawing.drawRoad(6163);
        do Drawing.drawBoxPlatform(7178);
        do Drawing.drawAlekseyTorso(4879);
        do Drawing.drawAlekseyHeadRight(3823);
        return;
    }

    function void printRules() {
        do Output.moveCursor(1, 0);
        do Output.printString("Time: 99");
        do Output.moveCursor(3, 13);
        do Output.printString("Press [W] [S] [L] [P] to catch Nandik!");
        do Output.moveCursor(4, 20);
        do Output.printString("Don't catch Impostors!!!");
        do Output.moveCursor(12, 4);
        do Output.printString("[W]");
        do Output.moveCursor(16, 4);
        do Output.printString("[S]");
        do Output.moveCursor(12, 57);
        do Output.printString("[P]");
        do Output.moveCursor(16, 57);
        do Output.printString("[L]");
        return;
    }
}