class GameField {

    function void run() {

        var char key, previousKey;

        var Aleksey aleksey;
        var int alekseyRotation;

        var Nandik nandik;
        var boolean nandikShouldMove;
        var boolean nandikCanSpawn;

        var int road, roadPosition;
        var RandomGenerator rng;

        var int moveTick;
        var int maxSpawnTick;
        var int spawnTick;

        var boolean exit;


        let key = 0;
        let previousKey = 0;

        let aleksey = Aleksey.new();
        let rng = RandomGenerator.new(91); 

        let nandikCanSpawn = true;
        let moveTick = 50;
        let maxSpawnTick = 200;
        let spawnTick = 100;

        let exit = false;


        while (~exit) {
            do Sys.wait(10);

            let spawnTick = spawnTick - 1;
            if (nandikCanSpawn) {
                let road = rng.random() + 1;
                let nandik = Nandik.new(road, moveTick);
                let nandikCanSpawn = false;
                let spawnTick = maxSpawnTick;
                if (moveTick > 10){ let moveTick = moveTick - 5; }
            }

            let previousKey = key;
            let key = Keyboard.keyPressed();

            if (~(key = previousKey)) { do aleksey.clearHands(); }

            if (~((key = 140) | (key = 87) | (key = 76) | (key = 83) | (key = 80))) { do aleksey.setRotation(0); }
            if (key = 87) { do aleksey.setRotation(1); } // W
            if (key = 80) { do aleksey.setRotation(2); } // P
            if (key = 83) { do aleksey.setRotation(3); } // S
            if (key = 76) { do aleksey.setRotation(4); } // L
            if (key = 140) { let exit = true; } // Escape
            
            let alekseyRotation = aleksey.getRotation();

            if (~(nandik = null)) { 
                let nandikShouldMove = nandik.tryMove(); 
                if (nandikShouldMove) { do nandik.move(alekseyRotation); }
                
                let roadPosition = nandik.getRoadPosition();
                if (roadPosition = 7) { 
                    do nandik.dispose(); 
                    let nandik = null;
                    let nandikCanSpawn = true;
                }
            }
            do aleksey.rotate();
        }
        do rng.dispose();
        return;
    }

    function void initialize() {
        do Drawing.drawRoad(4869);
        do Drawing.drawRoad(4884);
        do Drawing.drawRoad(6150);
        do Drawing.drawRoad(6163);
        do Drawing.drawBoxPlatform(7178);
        do Drawing.drawAlekseyTorso(4879);
        do Drawing.drawAlekseyHeadRight(3823);
        return;
    }
}