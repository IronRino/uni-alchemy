class GameField {

    function void run() {

        var Score score;

        var char key, previousKey;

        var Aleksey aleksey;
        var int alekseyRotation;

        var Obstacle obstacle;
        var boolean obstacleShouldMove;
        var boolean obstacleCanSpawn;

        var int road, roadPosition;
        var RandomGenerator rng;
        var int randomObstacle;

        var boolean isNandik;

        var int moveTick;

        var boolean exit;


        let score = Score.new();

        let key = 0;
        let previousKey = 0;

        let aleksey = Aleksey.new();
        let rng = RandomGenerator.new(91);

        let obstacleCanSpawn = true;
        let moveTick = 50;

        let exit = false;
        

        while (~exit) {
            do Sys.wait(10);
            if (obstacleCanSpawn) {
                let randomObstacle = rng.random(100);
                if (randomObstacle > 75) { let isNandik = false; }
                if (~(randomObstacle > 75)) { let isNandik = true; }

                let road = rng.random(4) + 1;
                let obstacle = Obstacle.new(road, moveTick, isNandik);
                let obstacleCanSpawn = false;
                if (moveTick > 10){ let moveTick = moveTick - 5; }
            }

            let previousKey = key;
            let key = Keyboard.keyPressed();
            if (~(key = previousKey)) { do aleksey.clearHands(); }

            if (~((key = 140) | (key = 87) | (key = 76) | (key = 83) | (key = 80))) { do aleksey.setRotation(0); }
            if (key = 87) { do aleksey.setRotation(1); } // W
            if (key = 80) { do aleksey.setRotation(2); } // P
            if (key = 83) { do aleksey.setRotation(3); } // S
            if (key = 76) { do aleksey.setRotation(4); } // L
            if (key = 140) { let exit = true; } // Escape
            
            let alekseyRotation = aleksey.getRotation();

            if (~(obstacle = null)) { 
                let obstacleShouldMove = obstacle.tryMove(); 
                if (obstacleShouldMove) { do obstacle.move(alekseyRotation, score); }
                
                let roadPosition = obstacle.getRoadPosition();
                if (roadPosition = 7) { 
                    do obstacle.dispose(); 
                    let obstacle = null;
                    let obstacleCanSpawn = true;
                }
            }

            do aleksey.rotate();
        }

        do rng.dispose();
        return;
    }

    function void initialize() {
        do Drawing.drawRoad(4869);
        do Drawing.drawRoad(4884);
        do Drawing.drawRoad(6150);
        do Drawing.drawRoad(6163);
        do Drawing.drawBoxPlatform(7178);
        do Drawing.drawAlekseyTorso(4879);
        do Drawing.drawAlekseyHeadRight(3823);
        return;
    }
}