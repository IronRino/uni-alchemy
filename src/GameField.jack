class GameField {

    function void run(int seed) {

        var char key, previousKey;

        var Score score;

        var Aleksey aleksey;
        var int alekseyRotation;

        var Random rng;
        var int randomValue;
        var int road, roadPosition;

        var Obstacle obstacle;
        var boolean obstacleShouldMove, obstacleCanSpawn, isNandik;
        var int moveTick;

        var boolean exit;


        let key = 3;
        let previousKey = 3;

        let score = Score.new();

        let aleksey = Aleksey.new();
        
        let rng = Random.new();
        let randomValue = seed;

        let obstacleCanSpawn = true;
        let moveTick = 50;

        let exit = false;
        

        while (~exit) {

            do Sys.wait(10);

            if (obstacleCanSpawn) {
                
                let randomValue = rng.getNext(randomValue, key + rng[randomValue]);
                if (randomValue < 13) { let isNandik = false; }
                else {let isNandik = true;}
                let road = rng[randomValue];


                let obstacle = Obstacle.new(road, moveTick, isNandik);
                let obstacleCanSpawn = false;
                if (moveTick > 10){ let moveTick = moveTick - 5; }
            }

            let previousKey = key;
            let key = Keyboard.keyPressed();
            if (~(key = previousKey)) { do aleksey.clearHands(); }

            if (key = 140) { let exit = true; } // Escape

            if (~((key = 140) | (key = 87) | (key = 76) | (key = 83) | (key = 80))) { do aleksey.setRotation(0); }
            if (key = 87) { do aleksey.setRotation(1); } // W
            if (key = 80) { do aleksey.setRotation(2); } // P
            if (key = 83) { do aleksey.setRotation(3); } // S
            if (key = 76) { do aleksey.setRotation(4); } // L
            let alekseyRotation = aleksey.getRotation();

            if (~(obstacle = null)) { 
                let obstacleShouldMove = obstacle.tryMove(); 
                if (obstacleShouldMove) { do obstacle.move(alekseyRotation, score); }
                
                let roadPosition = obstacle.getRoadPosition();
                if (roadPosition = 7) { 
                    do obstacle.dispose(); 
                    let obstacle = null;
                    let obstacleCanSpawn = true;
                }
            }

            do aleksey.rotate();
        }

        do rng.dispose();
        return;
    }

    function void initialize() {
        do Drawing.drawRoad(4869);
        do Drawing.drawRoad(4884);
        do Drawing.drawRoad(6150);
        do Drawing.drawRoad(6163);
        do Drawing.drawBoxPlatform(7178);
        do Drawing.drawAlekseyTorso(4879);
        do Drawing.drawAlekseyHeadRight(3823);
        return;
    }

    function void printRules() {
        do Output.moveCursor(3, 13);
        do Output.printString("Press [W] [S] [L] [P] to catch Nandik!");
        do Output.moveCursor(4, 20);
        do Output.printString("Don't catch Impostors!!!");
        do Output.moveCursor(12, 6);
        do Output.printString("[W]");
        do Output.moveCursor(16, 6);
        do Output.printString("[S]");
        do Output.moveCursor(12, 57);
        do Output.printString("[P]");
        do Output.moveCursor(16, 57);
        do Output.printString("[L]");
        return;
    }
}